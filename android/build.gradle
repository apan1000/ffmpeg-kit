apply plugin: 'maven-publish'

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Publishing
 */
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "6.0-3"
}

def getArtifactId = { ->
    return "ffmpeg-kit-min-gpl"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/apan1000/ffmpeg-kit")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId 'com.apan1000.ffmpegkit'
            artifactId getArtifactId()
            version getVersionName()
            artifact("$rootDir/../prebuilt/bundle-android-aar/ffmpeg-kit/${getArtifactId()}-${getVersionName()}.aar")
        }
    }
}
